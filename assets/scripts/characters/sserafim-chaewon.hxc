import funkin.play.character.AnimateAtlasCharacter;
import funkin.play.character.CharacterType;
import funkin.play.PlayState;
import flixel.FlxG;
import flixel.FlxSprite;
import flixel.group.FlxTypedSpriteGroup;
import animate.internal.elements.FlxSpriteElement;
import funkin.graphics.FunkinSprite;
import flixel.util.FlxSort;
import funkin.Conductor;
import funkin.util.SortUtil;
import Lambda;

class SserafimChaewonCharacter extends AnimateAtlasCharacter
{
  var lipSyncSprite:SserafimLipSyncSprite;

  /**
   * A map of animation names to lip sync data.
   * This is so it gets offset properly!
   */
  final LIP_SYNC_OFFSETS:Map<String, LipSyncData> = [
    'idle' =>
    {
      offset: [41, 3],
      angle: -166
    },
    'singUP' =>
    {
      offset: [38, 0],
      angle: -168
    },
    'singRIGHT' =>
    {
      offset: [39, 1],
      angle: -165
    },
    'singDOWN' =>
    {
      offset: [41, 3],
      angle: -167
    },
    'singLEFT' =>
    {
      offset: [40, 2],
      angle: -165
    }
  ];

  function new()
  {
    super('sserafim-chaewon');
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    lipSyncSprite = new SserafimLipSyncSprite(0, 0);
    lipSyncSprite.alpha = 0.5;

    var element:FlxSpriteElement = new FlxSpriteElement(lipSyncSprite);
    element.active = false; // We disable the element here so we can control when it updates.

    for (frame in this.getFramesWithKeyword("mouth default"))
    {
      frame.add(element);
    }
  }

  override public function playAnimation(name:String, restart:Bool, ignoreOther:Bool):Void
  {
    super.playAnimation(name, restart, ignoreOther);

    if (LIP_SYNC_OFFSETS.exists(name) && lipSyncSprite != null)
    {
      var data:LipSyncData = LIP_SYNC_OFFSETS.get(name);

      lipSyncSprite.offset.set(data.offset[0], data.offset[1]);
      lipSyncSprite.angle = data.angle;
    }
  }

  override function update(elapsed:Float):Void
  {
    super.update(elapsed);
    lipSyncSprite.update(elapsed);

    lipSyncSprite.shouldSing = this.characterType == CharacterType.BF;

    synchronizeShader();
  }

  var currentShader = null;

  function synchronizeShader():Void
  {
    if (currentShader == this.shader) return;

    currentShader = this.shader;

    lipSyncSprite.shader = currentShader;

    trace("Synchronized shader between children!");
  }

  override public function isOnScreen(camera:FlxCamera):Bool
  {
    // TODO: Figure out why she disappears when the camera zooms in too much!!
    // This is such a shit fix but it works.
    return true;
  }
}
