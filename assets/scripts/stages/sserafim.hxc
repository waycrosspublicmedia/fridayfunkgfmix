import flixel.addons.display.FlxBackdrop;
import flixel.FlxG;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.tweens.misc.ColorTween;
import flixel.util.FlxColor;
import flixel.util.FlxTimer;
import flixel.FlxSprite;
import flixel.util.FlxTimerManager;
import funkin.data.character.CharacterDataParser;
import funkin.graphics.shaders.DropShadowShader;
import funkin.graphics.shaders.SserafimShader;
import funkin.modding.base.ScriptedFlxSpriteGroup;
import funkin.play.character.BaseCharacter;
import flixel.text.FlxText;
import flixel.text.FlxTextBorderStyle;
import funkin.ui.FullScreenScaleMode;
import funkin.play.character.CharacterType;
import funkin.play.character.CharacterType;
import funkin.play.character.SparrowCharacter;
import funkin.play.PlayState;
import funkin.audio.FunkinSound;
import funkin.play.cutscene.CutsceneType;
import funkin.play.PlayStatePlaylist;
import funkin.play.stage.Stage;
import funkin.util.HapticUtil;
import funkin.util.Constants;
import funkin.util.TouchUtil;

class SserafimStage extends Stage
{
  var baseVisible:Array<Bool> = [true, false, false, false, false, false];
  var baseSinging:Array<Bool> = [false, false, false, false, false, false];

  // CHARACTERS
  var yunjin:BaseCharacter;
  var chaewon:BaseCharacter;
  var eunchae:BaseCharacter;

  // VFX/SHADERS
  var characterShader:SserafimShader;
  var stageShader:SserafimShader;

  // SPRITES
  var perspectiveFloor:PerspectiveSprite = null;

  // CUTSCENE SHIT
  var hasPlayedCutscene:Bool;

  var SEEYOU1:FlxSprite;
  var SEEYOU2:FlxSprite;

  function new()
  {
    super('sserafim');

    this.hasHidden = false;
    hasPlayedCutscene = false;
  }

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    this.hasHidden = false;
    hasPlayedCutscene = false;
    cutsceneSkipped = false;
    canSkipCutscene = false;
    cutsceneTimerManager = null;
  }

  function onDestroy(event:ScriptEvent):Void
  {
    super.onDestroy(event);

    this.hasHidden = false;
    hasPlayedCutscene = false;
  }

  var dust1:FlxBackdrop;
  var dust2:FlxBackdrop;
  var dust3:FlxBackdrop;
  var dust4:FlxBackdrop;

  function buildStage()
  {
    characterShader = new SserafimShader(true);
    stageShader = new SserafimShader();

    super.buildStage();

    perspectiveFloor = ScriptedFlxSpriteGroup.init('PerspectiveSprite');
    perspectiveFloor.sprite.loadGraphic(Paths.image('floor'));
    perspectiveFloor.setPositions(760, 1375, 790, 625);
    perspectiveFloor.setScrollFactors(1.05, 1.05, 0.93, 0.93);

    perspectiveFloor.zIndex = 11;
    PlayState.instance.currentStage.add(perspectiveFloor);

    dust1 = new FlxBackdrop(Paths.image('dust/dustMid'), 0x01);
    dust1.setPosition(-650, -200);
    dust1.scrollFactor.set(1.1, 1.1);
    dust1.scale.set(1.5, 1.5);
    dust1.zIndex = 2000;
    dust1.alpha = 0.8;
    dust1.velocity.x = 350;

    dust2 = new FlxBackdrop(Paths.image('dust/dustBack'), 0x01);
    dust2.setPosition(-650, -250);
    dust2.scrollFactor.set(1.15, 1.15);
    dust2.scale.set(1.5, 1.5);
    dust2.zIndex = 2000;
    dust2.alpha = 0.9;
    dust2.velocity.x = -300;

    dust3 = new FlxBackdrop(Paths.image('dust/dustMid'), 0x01);
    dust3.setPosition(-650, -400);
    dust3.scrollFactor.set(1.2, 1.2);
    dust3.scale.set(2, 2);
    dust3.zIndex = 2000;
    dust3.alpha = 0.8;
    dust3.velocity.x = -200;

    dust4 = new FlxBackdrop(Paths.image('dust/dustBack'), 0x01);
    dust4.setPosition(-650, -1300);
    dust4.scrollFactor.set(1.25, 1.25);
    dust4.scale.set(3.5, 3.5);
    dust4.zIndex = 2000;
    dust4.alpha = 0.9;
    dust4.velocity.x = -150;

    PlayState.instance.currentStage.add(dust1);
    PlayState.instance.currentStage.add(dust2);
    PlayState.instance.currentStage.add(dust3);
    PlayState.instance.currentStage.add(dust4);

    dust1.color = 0xff98847d;
    dust2.color = 0xff8b6c63;
    dust3.color = 0xff6e645c;
    dust4.color = 0xff886a60;

    yunjin = CharacterDataParser.fetchCharacter('sserafim-yunjin');
    chaewon = CharacterDataParser.fetchCharacter('sserafim-chaewon');
    eunchae = CharacterDataParser.fetchCharacter('sserafim-eunchae');

    PlayState.instance.currentStage.addCharacter(yunjin, CharacterType.OTHER);
    PlayState.instance.currentStage.addCharacter(chaewon, CharacterType.OTHER);
    PlayState.instance.currentStage.addCharacter(eunchae, CharacterType.OTHER);

    yunjin.zIndex = 1000;
    chaewon.zIndex = 1000;
    eunchae.zIndex = 1000;

    yunjin.scrollFactor.set(0.95, 0.95);
    chaewon.scrollFactor.set(0.95, 0.95);
    eunchae.scrollFactor.set(0.97, 0.97);

    yunjin.setPosition(-621 - yunjin.characterOrigin.x, 154 - yunjin.characterOrigin.y);
    chaewon.setPosition(687 - chaewon.characterOrigin.x, 98 - chaewon.characterOrigin.y);
    eunchae.setPosition(770 - eunchae.characterOrigin.x, 675 - eunchae.characterOrigin.y);

    setGirlsVisible(baseVisible);
    setGirlsSinging(baseSinging);
    setLightState(false);

    PlayState.instance.currentStage.refresh();

    perspectiveFloor.sprite.shader = stageShader;
    yunjin.shader = characterShader;
    chaewon.shader = characterShader;
    eunchae.shader = characterShader;

    createCutsceneSprites();
  }

  function onNoteHit(event:HitNoteScriptEvent)
  {
    super.onNoteHit(event);
    if (PlayState.instance.currentStage == null) return;
  }

  function hideOpponentStrumline()
  {
    var opponentStrumline:FlxSprite = PlayState.instance.opponentStrumline;
    if (opponentStrumline != null)
    {
      for (arrow in opponentStrumline.members)
      {
        arrow.visible = false;
      }
    }
  }

  /**
   * Called when the chart hits a song event.
   */
  public override function onSongEvent(scriptEvent:SongEventScriptEvent)
  {
    super.onSongEvent(scriptEvent);

    switch (scriptEvent.eventData.eventKind)
    {
      case 'sserafimGuitarVibration':
        HapticUtil.increasingVibrate(Constants.MIN_VIBRATION_AMPLITUDE, Constants.MAX_VIBRATION_AMPLITUDE / 2, scriptEvent.eventData.getFloat('duration'));
      case 'sserafimShow':
        setGirlsVisible(scriptEvent.eventData.getBoolArray('visible'));
      case 'sserafimSing':
        setGirlsSinging(scriptEvent.eventData.getBoolArray('singing'));
      case 'sserafimDark':
        setDarkenAmt(scriptEvent.eventData.getFloat('amount'), scriptEvent.eventData.getFloat('duration'));
      case 'sserafimLights':
        flashTruckLights(scriptEvent.eventData.getFloat('amount'), scriptEvent.eventData.getFloat('duration'));
      case 'sserafimCover':
        setCoverVisible(scriptEvent.eventData.getBool('visible'));
      case 'sserafimFlash':
        flashScreen(scriptEvent.eventData.getFloat('duration'));
      case 'sserafimPulseLights':
        setLightState(scriptEvent.eventData.getBool('enabled'), scriptEvent.eventData.getArray('colors'), scriptEvent.eventData.getArray('durations'),
          scriptEvent.eventData.getArray('intensities'));
      case 'sserafimKick':
        if (scriptEvent.eventData.getBool('final'))
        {
          // play second kick anim + reset her idle back to normal
          yunjin.playAnimation('kick2', true, false);
          FunkinSound.playOnce(Paths.sound('doorKick2'), 1.0);
          yunjin.danceEvery = 1;

          HapticUtil.vibrate(0, 0.2, 0.8, 0);

          // Show the opponent health icon at this point
          PlayState.instance.iconP2.visible = true;

          // hide the cutscene characters if theyre present!
          if (sserafimGf != null)
          {
            // and show the REAL gf
            PlayState.instance.currentStage.getGirlfriend()?.visible = true;

            sserafimGf.visible = false;
            sserafimBf.visible = false;
          }

          yunjin.animation.onFrameChange.removeAll();

          yunjin.animation.onFrameChange.add(function(animName:String, frameNumber:Int, index:Int) {
            // at this point in the animation, the door is no longer part of her animation...
            // show a static one!
            if (frameNumber == 23) getNamedProp('truckDoor').visible = true;
          });

          yunjin.animation.onFinish.addOnce(function(animName:String) {
            yunjin.animation.onFrameChange.removeAll();
          });

          // start the dust clearing
          startClear();
        }
        else
        {
          // play first kick anim
          yunjin.playAnimation('kick1', true, false);
          FunkinSound.playOnce(Paths.sound('doorKick1'), 1.0);

          HapticUtil.vibrate(0, 0.2, 0.4, 0);
        }
      case 'sserafimEnd':
        endStuff();
    }
  }

  function flashScreen(duration:Float)
  {
    PlayState.instance.camGame.flash(0xFFFFFFFF, duration);
  }

  function setCoverVisible(visible:Bool)
  {
    getNamedProp('solidCover').alpha = visible ? 1.0 : 0.0;
  }

  function setGirlsVisible(visibleArray:Array<Bool>)
  {
    if (visibleArray.length < 5) return;

    yunjin.visible = visibleArray[0];

    PlayState.instance.currentStage.getDad()?.visible = visibleArray[1];

    chaewon.visible = visibleArray[2];
    eunchae.visible = visibleArray[3];

    PlayState.instance.currentStage.getBoyfriend()?.visible = visibleArray[4];

    // gf visibility ISNT stored here, cause itd break a lot of stuff already in place and im kinda running out of time
  }

  function setGirlsSinging(singingArray:Array<Bool>)
  {
    if (singingArray.length < 5) return;

    yunjin.characterType = singingArray[0] ? CharacterType.BF : CharacterType.DAD;

    PlayState.instance.currentStage.getDad()?.characterType = singingArray[1] ? CharacterType.BF : CharacterType.DAD;

    chaewon.characterType = singingArray[2] ? CharacterType.BF : CharacterType.DAD;
    eunchae.characterType = singingArray[3] ? CharacterType.BF : CharacterType.DAD;

    PlayState.instance.currentStage.getBoyfriend()?.characterType = singingArray[4] ? CharacterType.BF : CharacterType.DAD;

    PlayState.instance.currentStage.getGirlfriend()?.characterType = singingArray[5] ? CharacterType.BF : CharacterType.DAD;
  }

  override function addCharacter(character:BaseCharacter, charType:CharacterType):Void
  {
    super.addCharacter(character, charType);

    var targetIndex:Int = 0;
    switch (character.characterId)
    {
      case 'sserafim-yunjin':
        targetIndex = 0;
      case 'sserafim-kazuha':
        targetIndex = 1;
      case 'sserafim-chaewon':
        targetIndex = 2;
      case 'sserafim-eunchae':
        targetIndex = 3;
      case 'sserafim-sakura':
        targetIndex = 4;
      case 'sserafim-gf':
        targetIndex = 5;
      default:
    }

    character.characterType = baseSinging[targetIndex] ? CharacterType.BF : CharacterType.DAD;
    character.visible = baseVisible[targetIndex];

    character.shader = characterShader;
  }

  function onSongRetry(event:ScriptEvent)
  {
    super.onSongRetry(event);
    setCoverVisible(false);

    setGirlsVisible(baseVisible);
    setGirlsSinging(baseSinging);
    setLightState(false);

    resetClear();

    FlxTween.cancelTweensOf(characterShader);
    FlxTween.cancelTweensOf(stageShader);

    characterShader.darkenAmount = 0;
    stageShader.darkenAmount = 0;

    // Reapply the shader when the song restarts since the game over clears it!
    PlayState.instance.currentStage.getBoyfriend().shader = characterShader;

    // Hide the opponent health icon
    PlayState.instance.iconP2.visible = false;

    if (sserafimCutscene != null) playCutsceneFromRestart();

    getNamedProp('solidCover').alpha = 0.0;
    getNamedProp('truckLight1').alpha = 0.0;
    getNamedProp('truckLight2').alpha = 0.0;
    getNamedProp('backLightColor').alpha = 0.0;
    getNamedProp('backLightWhite').alpha = 0.0;
  }

  var hasHidden = false;

  override function addProp(prop:StageProp, ?name:String = null)
  {
    super.addProp(prop, name);
    prop.shader = stageShader;

    switch (name)
    {
      case 'truckLight1':
        prop.shader = null;

        prop.blend = 12;
      case 'truckLight2':
        prop.shader = null;

      case 'backLightColor':
        prop.shader = null;

        prop.blend = 12;
        prop.color = 0xFFCC3300;
      case 'backLightWhite':
        prop.shader = null;

        prop.blend = 0;
      case 'truckTest':
        prop.shader = null;
      case 'solidCover':
        prop.shader = null;
    }
  }

  function setDarkenAmt(darkAmt:Float, duration:Float)
  {
    FlxTween.cancelTweensOf(characterShader);
    FlxTween.cancelTweensOf(stageShader);

    FlxTween.tween(characterShader, {darkenAmount: darkAmt}, duration, {ease: FlxEase.sineInOut});
    FlxTween.tween(stageShader, {darkenAmount: darkAmt}, duration, {ease: FlxEase.sineInOut});
  }

  function flashTruckLights(amount:Float, duration:Float):Void
  {
    FlxTween.cancelTweensOf(getNamedProp('truckLight1'));
    FlxTween.cancelTweensOf(getNamedProp('truckLight2'));

    HapticUtil.vibrate(0, duration / 2, amount / 2, 0);

    getNamedProp('truckLight1').alpha = amount;
    getNamedProp('truckLight2').alpha = amount;

    characterShader.truckLightStrength = amount;
    stageShader.truckLightStrength = amount;

    FlxTween.tween(getNamedProp('truckLight1'), {alpha: 0}, duration,
      {
        ease: FlxEase.cubeInOut,
        onUpdate: function(tween:FlxTween) {
          characterShader.truckLightStrength = getNamedProp('truckLight1').alpha;
          stageShader.truckLightStrength = getNamedProp('truckLight1').alpha;
        },
        onComplete: function(tween:FlxTween) {
          characterShader.truckLightStrength = 0;
          stageShader.truckLightStrength = 0;
        }
      });
    FlxTween.tween(getNamedProp('truckLight2'), {alpha: 0}, duration, {ease: FlxEase.cubeInOut});
  }

  var lightsColors:Array<FlxColor> = [];
  var lightsDurations:Array<Float> = [];
  var lightsIntensities:Array<Float> = [];
  var lightsEnabled:Bool = false;

  function setLightState(enabled:Bool = false, ?colors:Array<String>, ?durations:Array<Float>, ?intensities:Array<Float>)
  {
    lightsEnabled = enabled;
    if (colors == null || durations == null || intensities == null) return;

    lightsColors = [for (i in 0...colors.length) FlxColor.fromString(colors[i])];
    lightsDurations = durations;
    lightsIntensities = intensities;
  }

  function flashBackLight(amount:Float, duration:Float, color:FlxColor)
  {
    FlxTween.cancelTweensOf(getNamedProp('backLightColor'));
    FlxTween.cancelTweensOf(getNamedProp('backLightWhite'));

    getNamedProp('backLightColor').color = color;

    getNamedProp('backLightColor').alpha = amount * 0.8;
    getNamedProp('backLightWhite').alpha = amount * 0.7;

    characterShader.pulseLightColor = color;
    stageShader.pulseLightColor = color;

    characterShader.pulseLightStrength = getNamedProp('backLightColor').alpha;
    stageShader.pulseLightStrength = getNamedProp('backLightColor').alpha;

    FlxTween.tween(getNamedProp('backLightColor'), {alpha: 0}, duration,
      {
        ease: FlxEase.cubeInOut,
        onUpdate: function(tween:FlxTween) {
          characterShader.pulseLightStrength = getNamedProp('backLightColor').alpha;
          stageShader.pulseLightStrength = getNamedProp('backLightColor').alpha;
        },
        onComplete: function(tween:FlxTween) {
          characterShader.pulseLightStrength = 0;
          stageShader.pulseLightStrength = 0;
        }
      });
    FlxTween.tween(getNamedProp('backLightWhite'), {alpha: 0}, duration, {ease: FlxEase.cubeInOut});
  }

  function onBeatHit(event:SongTimeScriptEvent)
  {
    super.onBeatHit(event);

    // flash lights behind truck
    if (lightsEnabled) flashBackLight(lightsIntensities[event.beat % lightsIntensities.length], lightsDurations[event.beat % lightsDurations.length],
      lightsColors[event.beat % lightsColors.length]);
  }

  var isMobilePauseButtonPressed:Bool = false;

  function onUpdate(event:UpdateScriptEvent):Void
  {
    super.onUpdate(event);
    if (cutsceneTimerManager != null) cutsceneTimerManager.update(event.elapsed);

    if (!this.hasHidden)
    {
      this.hasHidden = true;
      hideOpponentStrumline();
      // PlayState.instance.comboPopUps.offsets = [510, 320]; actual values for if we ever wanna use it
      PlayState.instance.comboPopUps.offsets = [9999, -50];
    }

    if (perspectiveFloor != null)
    {
      perspectiveFloor.updateSkew(PlayState.instance.camGame);
    }

    if (FlxG.onMobile)
    {
      isMobilePauseButtonPressed = TouchUtil.overlapsComplex(PlayState.instance.pauseButton) && TouchUtil.justPressed;
    }

    if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && !cutsceneSkipped)
    {
      if (!canSkipCutscene)
      {
        trace('cant skip yet!');
        if (skipText != null)
        {
          FlxTween.tween(skipText, {alpha: 1}, 0.5, {ease: FlxEase.quadOut});
          new FlxTimer().start(0.5, _ -> {
            canSkipCutscene = true;
            trace('can skip!');
          });
        }
      }
    }
    if ((PlayState.instance.controls.CUTSCENE_ADVANCE || isMobilePauseButtonPressed) && !cutsceneSkipped && canSkipCutscene)
    {
      skipCutscene();
      trace('skipped');
    }

    // i cant bear the weight of deleting you debugShitLol you can stay here just so i can remember how useful you were
    // debugShitLol();
  }

  // ----------------- CUTSCENE LOGIC -----------------
  var sserafimCutscene:ScriptedFunkinSprite;
  var sserafimBf:ScriptedFunkinSprite;
  var sserafimGf:ScriptedFunkinSprite;
  var cutsceneSounds:Null<FunkinSound> = null;

  var skipText:FlxText;
  var cutsceneSkipped:Bool = false;
  var canSkipCutscene:Bool = false;
  var cutsceneTimerManager:FlxTimerManager;

  public function cancelCutsceneSounds():Void
  {
    if (cutsceneSounds != null) cutsceneSounds.destroy();
  }

  public override function onCountdownStart(event:CountdownScriptEvent):Void
  {
    super.onCountdownStart(event);

    if (PlayState.instance.isChartingMode && !hasPlayedCutscene)
    {
      hasPlayedCutscene = true;
      cutsceneSkipped = true;
      playCutsceneFromRestart();
    }

    if (!hasPlayedCutscene)
    {
      trace('Pausing countdown to play in game cutscene');

      hasPlayedCutscene = true;

      event.cancel(); // CANCEL THE COUNTDOWN!

      PlayState.instance.camHUD.visible = false;

      setCutsceneVisibility(true);
      introCutscene();
    }
  }

  // helper function cause i really dont wanna have to manually do this twice
  function setCutsceneVisibility(inCutscene:Bool):Void
  {
    if (inCutscene)
    {
      setGirlsVisible([!inCutscene, !inCutscene, !inCutscene, !inCutscene, !inCutscene]);
    }
    else
    {
      setGirlsVisible(baseVisible);
    }

    getNamedProp('truck').visible = !inCutscene;
    getNamedProp('truckDoor').visible = false;
    getNamedProp('backTables').visible = !inCutscene;
    getNamedProp('backStools').visible = !inCutscene;
    getNamedProp('frontStool').visible = !inCutscene;
    hideDust(!inCutscene);

    perspectiveFloor.sprite.loadGraphic(Paths.image(inCutscene ? 'cutscene/floor-cutscene' : 'floor'));

    getNamedProp('backTablesCutscene').visible = inCutscene;
    getNamedProp('burgerCutscene').visible = inCutscene;

    // gf will be hidden at the start + in the cutscene so we can just do this here
    PlayState.instance.currentStage.getGirlfriend()?.visible = false;

    if (sserafimCutscene != null) sserafimCutscene.visible = inCutscene;
  }

  function createCutsceneSprites()
  {
    sserafimCutscene = new SserafimCutsceneSprite(0, 0);
    sserafimCutscene.setPosition(-395, 10);
    sserafimCutscene.scrollFactor.set(0.94, 0.94);
    sserafimCutscene.shader = characterShader;
    sserafimCutscene.zIndex = 25;

    sserafimGf = new SserafimGfSprite(0, 0);
    sserafimGf.setPosition(655, -104);
    sserafimGf.scrollFactor.set(0.95, 0.95);
    sserafimGf.shader = characterShader;
    sserafimGf.alpha = 0.5;
    sserafimGf.zIndex = 25;
    sserafimGf.visible = false;

    sserafimBf = new SserafimBfSprite(0, 0);
    sserafimBf.setPosition(1220, 531);
    sserafimBf.scrollFactor.set(0.99, 0.99);
    sserafimBf.shader = characterShader;
    sserafimBf.zIndex = 305;
    sserafimBf.visible = false;

    PlayState.instance.currentStage.add(sserafimGf);
    PlayState.instance.currentStage.add(sserafimBf);
    PlayState.instance.currentStage.add(sserafimCutscene);
    PlayState.instance.currentStage.refresh();

    SEEYOU1 = new FlxSprite().loadGraphic(Paths.image('end/end1'));
    SEEYOU1.scale.set(0.67, 0.67);
    SEEYOU1.updateHitbox();
    SEEYOU1.screenCenter();
    SEEYOU1.zIndex = 10000;

    SEEYOU2 = new FlxSprite().loadGraphic(Paths.image('end/end2'));
    SEEYOU2.scale.set(0.67, 0.67);
    SEEYOU2.updateHitbox();
    SEEYOU2.setPosition(FlxG.width - 40 - SEEYOU2.width, FlxG.height - 40 - SEEYOU2.height);
    SEEYOU2.zIndex = 10000;

    PlayState.instance.currentStage.add(SEEYOU1);
    PlayState.instance.currentStage.add(SEEYOU2);

    SEEYOU1.cameras = [PlayState.instance.camCutscene];
    SEEYOU2.cameras = [PlayState.instance.camCutscene];

    SEEYOU1.visible = false;
    SEEYOU2.visible = false;

    PlayState.instance.currentStage.refresh();
  }

  function endStuff()
  {
    FunkinSound.playOnce(Paths.sound('cutscene/end1'), 1.0);

    new FlxTimer().start(0.05, function(tmr) {
      HapticUtil.vibrate(0, 0.2, 0.4, 0);

      SEEYOU1.visible = true;
      getNamedProp('solidCover').alpha = 1;
      PlayState.instance.camHUD.visible = false;
      PlayState.instance.togglePauseButton();
      PlayState.instance.isInCutscene = true;
      PlayState.instance.mayPauseGame = false;
    });

    new FlxTimer().start(4, function(tmr) {
      SEEYOU1.visible = false;
      SEEYOU2.visible = true;
      FunkinSound.playOnce(Paths.sound('cutscene/end2'), 1.0);

      HapticUtil.vibrate(0, 0.4, 0.5, 0);
    });

    new FlxTimer().start(8, function(tmr) {
      SEEYOU1.visible = false;
      SEEYOU2.visible = false;
    });

    new FlxTimer().start(9, function(tmr) {
      PlayState.instance.endSong(true);
    });
  }

  function playCutsceneFromRestart()
  {
    setCutsceneVisibility(false);
    resetClear();

    PlayState.instance.tweenCameraZoom(0.55, 1, true, FlxEase.circOut);
    PlayState.instance.tweenCameraToPosition(1070, 470, 1, FlxEase.circOut);

    sserafimGf.visible = true;
    sserafimBf.visible = true;

    sserafimGf.doAnim();
    sserafimBf.doAnim();

    sserafimGf.animation.curAnim.curFrame = 23;
    sserafimBf.animation.curAnim.curFrame = 23;
  }

  function introCutscene()
  {
    PlayState.instance.isInCutscene = true;
    PlayState.instance.mayPauseGame = false;

    skipText = new FlxText(936 * FullScreenScaleMode.wideScale.x, 618 * FullScreenScaleMode.wideScale.y, 0,
      'Skip [ ' + PlayState.instance.controls.getDialogueNameFromToken("CUTSCENE_ADVANCE", true) + ' ]', 20);
    if (FlxG.onMobile)
    {
      skipText.text = 'Skip [Pause Button]';
      skipText.x -= 136;
    }
    skipText.setFormat(Paths.font('vcr.ttf'), 40, 0xFFFFFFFF, "right", FlxTextBorderStyle.OUTLINE, 0xFF000000);
    skipText.scrollFactor.set();
    skipText.borderSize = 2;
    skipText.alpha = 0;
    add(skipText);

    skipText.cameras = [PlayState.instance.camCutscene];

    cutsceneTimerManager = new FlxTimerManager();

    sserafimCutscene.doAnim();

    PlayState.instance.tweenCameraZoom(0.5, 0, true);
    PlayState.instance.tweenCameraToPosition(660, -200, 0);

    PlayState.instance.camGame.fade(0xFF000000, 3, true, null, true);

    new FlxTimer(cutsceneTimerManager).start(20 / 24, function(tmr) {
      cutsceneSounds = FunkinSound.load(Paths.sound('cutscene/startCutscene'), 1.0, false, true, true);
    });

    PlayState.instance.tweenCameraZoom(0.7, 3, true, FlxEase.circOut);
    PlayState.instance.tweenCameraToPosition(660, 300, 3, FlxEase.circOut);

    // gf taps herself
    new FlxTimer(cutsceneTimerManager).start(245 / 24, function(tmr) {
      HapticUtil.vibrate(0, 0.1, 0.2, 0);

      setDarkenAmt(0.2, 0.01);
    });
    new FlxTimer(cutsceneTimerManager).start(251 / 24, function(tmr) {
      setDarkenAmt(0, 0.8);
    });

    // gf taps bf
    new FlxTimer(cutsceneTimerManager).start(406 / 24, function(tmr) {
      HapticUtil.vibrate(0, 0.1, 0.2, 0);

      setDarkenAmt(0.2, 0.01);
    });
    new FlxTimer(cutsceneTimerManager).start(411 / 24, function(tmr) {
      setDarkenAmt(0, 0.8);
    });

    // truck starts getting closer
    new FlxTimer(cutsceneTimerManager).start(499 / 24, function(tmr) {
      cutsceneSkipped = true;
      canSkipCutscene = false;
      FlxTween.tween(skipText, {alpha: 0}, 0.5,
        {
          ease: FlxEase.quadIn,
          onComplete: _ -> {
            skipText.visible = false;
          }
        });
      // cutting off skipping here. really dont think its needed after this point and it saves problems from happening

      FlxTween.cancelTweensOf(stageShader);
      FlxTween.cancelTweensOf(characterShader);

      FlxTween.tween(stageShader,
        {
          baseBrightness: 55,
          baseHue: 0,
          baseContrast: -30,
          baseSaturation: 0
        }, 49 / 24, {ease: FlxEase.sineOut});
      FlxTween.tween(characterShader,
        {
          baseBrightness: 55,
          baseHue: 0,
          baseContrast: -30,
          baseSaturation: 0
        }, 49 / 24, {ease: FlxEase.sineOut});
      HapticUtil.vibrate(10 / 24, 50 / 24, 0.1, 0.1);
    });

    // truck gets really close
    new FlxTimer(cutsceneTimerManager).start(548 / 24, function(tmr) {
      FlxTween.cancelTweensOf(stageShader);
      FlxTween.cancelTweensOf(characterShader);

      FlxTween.tween(stageShader,
        {
          baseBrightness: 66,
          baseHue: 10,
          baseContrast: -17,
          baseSaturation: 0
        }, 15 / 24, {ease: FlxEase.expoIn});
      FlxTween.tween(characterShader,
        {
          baseBrightness: 66,
          baseHue: 10,
          baseContrast: -17,
          baseSaturation: 0
        }, 15 / 24, {ease: FlxEase.expoIn});
      HapticUtil.vibrate(2 / 24, 20 / 24, 0.4, 0.4);
    });

    var offsetLol:Float = 562;

    // car crash + flash
    new FlxTimer(cutsceneTimerManager).start(563 / 24, function(tmr) {
      FlxTween.cancelTweensOf(stageShader);
      FlxTween.cancelTweensOf(characterShader);

      stageShader.setAdjustColor(0, 0, 0, 0);
      characterShader.setAdjustColor(0, 0, 0, 0);
      PlayState.instance.camGame.fade(0xFFFFFFFF, 30 / 24, true, null, true);
      getNamedProp('solidCover').alpha = 1;

      setCutsceneVisibility(false);

      new FlxTimer().start(5 / 24, function(tmr) {
        HapticUtil.vibrate(0, 10 / 24, 1, 1);
      });
    });

    // fade out from black and add dust
    new FlxTimer(cutsceneTimerManager).start(650 / 24, function(tmr) {
      PlayState.instance.tweenCameraZoom(0.7, 0, true);
      PlayState.instance.tweenCameraZoom(0.55, 3, true, FlxEase.circOut);
      PlayState.instance.tweenCameraToPosition(1070, 470, 0);

      resetClear();
      FlxTween.tween(getNamedProp('solidCover'), {alpha: 0}, 3, {ease: FlxEase.sineOut});

      sserafimGf.visible = true;
      sserafimBf.visible = true;
      sserafimGf.resetAnim();
      sserafimBf.resetAnim();
    });

    new FlxTimer(cutsceneTimerManager).start(710 / 24, function(tmr) {
      sserafimGf.doAnim();
      sserafimBf.doAnim();
    });

    // actually start song
    new FlxTimer(cutsceneTimerManager).start(730 / 24, function(tmr) {
      PlayState.instance.isInCutscene = false;
      PlayState.instance.startCountdown();

      PlayState.instance.togglePauseButton(true);
      PlayState.instance.isInCutscene = false;
      PlayState.instance.mayPauseGame = true;
    });
  }

  function skipCutscene()
  {
    cutsceneSkipped = true;
    hasPlayedCutscene = true;
    PlayState.instance.camCutscene.fade(0xFF000000, 0.5, false, null, true);

    if (cutsceneSounds != null) cutsceneSounds.fadeOut(0.5, 0);

    new FlxTimer().start(0.5, _ -> {
      PlayState.instance.justUnpaused = true;
      PlayState.instance.camCutscene.fade(0xFF000000, 0.5, true, null, true);

      cutsceneTimerManager.clear();
      cutsceneSounds.stop();

      PlayState.instance.isInCutscene = false;
      PlayState.instance.mayPauseGame = true;
      PlayState.instance.startCountdown();

      skipText.visible = false;

      playCutsceneFromRestart();
    });
  }

  function hideDust(visible:Bool)
  {
    if (dust1 == null) return;

    dust1.visible = visible;
    dust2.visible = visible;
    dust3.visible = visible;
    dust4.visible = visible;
  }

  function resetClear()
  {
    yunjin.playAnimation('doorclosed', true, true);
    yunjin.danceEvery = 0;

    getNamedProp('truckDoor').visible = false;

    for (thing in [
      stageShader,
      characterShader,
      dust1,
      dust1.velocity,
      dust2,
      dust2.velocity,
      dust3,
      dust3.velocity,
      dust4,
      dust4.velocity
    ])
    {
      FlxTween.cancelTweensOf(thing);
    }

    stageShader.setAdjustColor(-24, 6, -26, -74);
    characterShader.setAdjustColor(-24, 6, -26, -74);

    dust1.setPosition(-650, -400);
    dust2.setPosition(-650, -450);
    dust3.setPosition(-650, -600);
    dust4.setPosition(-650, -1500);

    dust1.velocity.x = 350;
    dust2.velocity.x = -300;
    dust3.velocity.x = -200;
    dust4.velocity.x = -150;

    dust1.alpha = 1;
    dust2.alpha = 1;
    dust3.alpha = 1;
    dust4.alpha = 1;
  }

  function startClear()
  {
    FlxTween.tween(stageShader,
      {
        baseBrightness: 0,
        baseHue: 0,
        baseContrast: 0,
        baseSaturation: 0
      }, 6.0 * 4, {ease: FlxEase.sineOut});
    FlxTween.tween(characterShader,
      {
        baseBrightness: 0,
        baseHue: 0,
        baseContrast: 0,
        baseSaturation: 0
      }, 6.0 * 4, {ease: FlxEase.sineOut});

    FlxTween.tween(dust1, {alpha: 0, y: dust1.y + 100}, 5.0 * 4, {ease: FlxEase.sineOut});
    FlxTween.tween(dust2, {alpha: 0, y: dust2.y + 200}, 4.0 * 4, {ease: FlxEase.sineOut});
    FlxTween.tween(dust3, {alpha: 0, y: dust3.y + 150}, 6.0 * 4, {ease: FlxEase.sineOut});
    FlxTween.tween(dust4, {alpha: 0, y: dust4.y + 100}, 4.0 * 4, {ease: FlxEase.sineOut});

    FlxTween.tween(dust1.velocity, {x: 0}, 5.0 * 4, {ease: FlxEase.sineOut});
    FlxTween.tween(dust2.velocity, {x: 0}, 4.0 * 4, {ease: FlxEase.sineOut});
    FlxTween.tween(dust3.velocity, {x: 0}, 6.0 * 4, {ease: FlxEase.sineOut});
    FlxTween.tween(dust4.velocity, {x: 0}, 4.0 * 4, {ease: FlxEase.sineOut});
  }
}
