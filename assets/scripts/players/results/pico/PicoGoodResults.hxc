import flixel.FlxG;
import funkin.graphics.FunkinSprite;
import funkin.util.HapticUtil;

class PicoGoodResults extends FunkinSprite
{
  /**
   * The offset for the white and black flashes.
   * This is to make sure they look right!!
   */
  final FLASH_POSITION_OFFSET:Float = 500;

  function new(x:Float, y:Float)
  {
    super(x, y);
    loadTextureAtlas("resultScreen/results-pico/resultsGOOD", "shared");
    scaleFlashes();

    animation.onFrameChange.add((animation:String, frame:Int) -> {
      if (animation == "pico good placeholder" && this.visible)
      {
        overrideAnimation();
        return;
      }
    });

    animation.onFrameLabel.add((label:String) -> {
      if (label == "flash frame" && HapticUtil.hapticsAvailable)
      {
        HapticUtil.vibrate(0, 0.01, 1);
      }
    });

    animation.onFinish.add((name:String) -> {
      if (name == "pico good placeholder") return;

      trace('Looping Pico shooting animation...');
      animation.play("loop", true);
      animation.curAnim.looped = true;
    });
  }

  /**
   * Overrides the default animation to play a different one:
   *
   * - 5% chance to play the fatass GF animation.
   * - 20% chance to play the Cassandra animation.
   * - Else, play the default animation.
   */
  function overrideAnimation():Void
  {
    if (FlxG.random.bool(5))
    {
      trace('Playing special Fatass animation...');
      animation.play("intro fat gf", true, false, 0);
    }
    else if (FlxG.random.bool(20))
    {
      trace('Playing special Cass animation...');
      animation.play("intro cass", true, false, 0);
    }
    else
    {
      trace('Playing Pico shooting animation...');
      animation.play("intro", true, false, 0);
    }
  }

  /**
   * Scales the white/black flash symbols to normal size.
   * They're tiny in the FLA for optimization purposes!
   */
  function scaleFlashes():Void
  {
    var whiteElement:Element = getFirstElement("white small");
    var blackElement:Element = getFirstElement("blacksmall");

    scaleElement(whiteElement, 10, FLASH_POSITION_OFFSET);
    scaleElement(blackElement, 15, FLASH_POSITION_OFFSET);
  }
}
